import com.amazonaws.auth.BasicAWSCredentials
import com.amazonaws.auth.profile.ProfileCredentialsProvider

buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.amazonaws:aws-java-sdk-core:1.11.5'
    }
}

plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
}

ext {
    awsCredentials = new ProfileCredentialsProvider().credentials as BasicAWSCredentials
}

repositories {
    mavenCentral()
}

dependencies {
    implementation localGroovy()
    implementation gradleApi()
    implementation 'com.amazonaws:aws-java-sdk-core:1.12.322'
}

gradlePlugin {
    plugins {
        araragiPlugin {
            id = 'koyomi-araragi'
            implementationClass = 'tv.isshoni.araragi.plugin.AraragiPlugin'
        }
    }
}

java {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
}

group = 'tv.isshoni.araragi'
version = "$plugin_version"
description = 'plugin'

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "${project.group}"
            artifactId = "${project.name}"
            version = "${project.version}"

            from components.java
        }
    }

    repositories {
        mavenLocal()
        maven {
            url 's3://repo.isshoni.institute'
            credentials(AwsCredentials) {
                accessKey awsCredentials.AWSAccessKeyId
                secretKey awsCredentials.AWSSecretKey
            }
        }
    }
}
